apply plugin: 'com.android.application'
//wrap with try and catch so the build is working even if the signing stuff is missing
try {
    apply from: '../../../signing.gradle'
} catch (ex) {
}


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 23
        versionCode 559
        versionName '5.5.9'

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def file = output.outputFile
                def fileName = file.name.replace(".apk", "-v" + versionName + "-c" + versionCode + ".apk")
                output.outputFile = new File(file.parentFile, fileName)
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            try {
                signingConfig signingConfigs.debug
            } catch (ex) {
            }
            minifyEnabled false
        }
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ex) {
            }
            zipAlignEnabled true
            minifyEnabled false
        }
    }
    productFlavors {
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.fima.cardsui:library:1.+@aar'
    compile project(':library')

    // used to base on some backwards compatible themes
    // contains util classes to support various android versions, and clean up code
    // comes with the awesome "Holder"-Pattern
    // https://github.com/mikepenz/Materialize
    compile 'com.mikepenz:materialize:0.8.6@aar'

    //used to generate the drawer on the left
    //https://github.com/mikepenz/MaterialDrawer
    compile('com.mikepenz:materialdrawer:5.1.4.1-SNAPSHOT@aar') {
        transitive = true
        exclude module: "fastadapter"
    }

    //used to provide different itemAnimators for the RecyclerView
    //https://github.com/mikepenz/ItemAnimators
    compile 'com.mikepenz:itemanimators:0.2.2@aar'

    //used to display the icons in the drawer
    //https://github.com/mikepenz/Android-Iconics
    compile 'com.mikepenz:material-design-iconic-typeface:2.2.0.1@aar'
}
