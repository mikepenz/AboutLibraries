apply plugin: 'com.android.application'
//wrap with try and catch so the build is working even if the signing stuff is missing
try {
    apply from: '../../../signing.gradle'
} catch (ignored) {
}


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 6080
        versionName '6.0.8'

        setProperty("archivesBaseName", "AboutLibraries-v$versionName-c$versionCode")
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            try {
                signingConfig signingConfigs.debug
            } catch (ignored) {
            }
            minifyEnabled false
        }
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ignored) {
            }
            zipAlignEnabled true
            minifyEnabled false
        }
    }
    productFlavors {
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation project(':library')

    implementation "com.android.support:design:${rootProject.ext.supportLibVersion}"
    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${rootProject.ext.supportLibVersion}"
    implementation "com.android.support:cardview-v7:${rootProject.ext.supportLibVersion}"

    // used to base on some backwards compatible themes
    // contains util classes to support various android versions, and clean up code
    // comes with the awesome "Holder"-Pattern
    // https://github.com/mikepenz/Materialize
    implementation 'com.mikepenz:materialize:1.1.2'

    //used to generate the drawer on the left
    //https://github.com/mikepenz/MaterialDrawer
    implementation 'com.mikepenz:materialdrawer:6.0.7'

    //used to provide different itemAnimators for the RecyclerView
    //https://github.com/mikepenz/ItemAnimators
    implementation 'com.mikepenz:itemanimators:1.0.2'

    // used to provide out of the box icon font support. simplifies development,
    // and provides scalable icons. the core is very very light
    // https://github.com/mikepenz/Android-Iconics
    implementation 'com.mikepenz:iconics-core:3.0.4'

    //used to display the icons in the drawer
    //https://github.com/mikepenz/Android-Iconics
    implementation 'com.mikepenz:material-design-iconic-typeface:2.2.0.3@aar'

}

configurations.all {
    resolutionStrategy.force "com.mikepenz:fastadapter:3.2.7"
    resolutionStrategy.force "com.mikepenz:iconics-core:3.0.4"
    resolutionStrategy.force "com.android.support:support-v4:${rootProject.ext.supportLibVersion}"
    resolutionStrategy.force "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    resolutionStrategy.force "com.android.support:cardview-v7:${rootProject.ext.supportLibVersion}"
    resolutionStrategy.force "com.android.support:recyclerview-v7:${rootProject.ext.supportLibVersion}"
    resolutionStrategy.force "com.android.support:design:${rootProject.ext.supportLibVersion}"
    resolutionStrategy.force "com.android.support:support-annotations:${rootProject.ext.supportLibVersion}"
}
