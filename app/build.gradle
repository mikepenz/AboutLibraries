apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.mikepenz.aboutlibraries.plugin' // has to be applied AFTER android

if (getSigningFile() != null) {
    apply from: "${getSigningFile()}"
}

android {
    compileSdkVersion setup.compileSdk
    buildToolsVersion setup.buildTools

    defaultConfig {
        minSdkVersion setup.minSdk
        targetSdkVersion setup.targetSdk
        versionCode release.versionCode
        versionName release.versionName

        setProperty("archivesBaseName", "AboutLibraries-v$versionName-c$versionCode")
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            if (getSigningFile() != null) {
                signingConfig signingConfigs.debug
            }
            minifyEnabled false
        }
        release {
            if (getSigningFile() != null) {
                signingConfig signingConfigs.release
            }
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        staging {
            if (getSigningFile() != null) {
                signingConfig signingConfigs.release
            }
            applicationIdSuffix ".debugStaging"
            matchingFallbacks = ['debug']
        }
    }
    productFlavors {
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        exclude 'META-INF/library-core_release.kotlin_module'
        exclude 'META-INF/library_release.kotlin_module'
    }
}

// It is possible to define a custom config path with custom mappings
aboutLibraries {
    configPath = "config"

    // Full license text for license IDs mentioned here
    // will be included, even if no detected dependency uses them.
    additionalLicenses {
        mit
        mpl_2_0
    }
}

dependencies {
    implementation project(':library')

    implementation "com.google.android.material:material:${versions.material}"
    implementation "androidx.appcompat:appcompat:${versions.appcompat}"
    implementation "androidx.recyclerview:recyclerview:${versions.recyclerview}"
    implementation "androidx.cardview:cardview:${versions.cardview}"

    //used to generate the drawer on the left
    //https://github.com/mikepenz/MaterialDrawer
    implementation "com.mikepenz:materialdrawer:${versions.materialdrawer}"

    //used to provide different itemAnimators for the RecyclerView
    //https://github.com/mikepenz/ItemAnimators
    implementation 'com.mikepenz:itemanimators:1.1.0'

    // used to provide out of the box icon font support. simplifies development,
    // and provides scalable icons. the core is very very light
    // https://github.com/mikepenz/Android-Iconics
    implementation "com.mikepenz:iconics-core:${versions.iconics}"

    //used to display the icons in the drawer
    //https://github.com/mikepenz/Android-Iconics
    implementation "com.mikepenz:material-design-iconic-typeface:2.2.0.8-kotlin@aar"

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"

    // used only tho showcase multi flavor support
    stagingImplementation("com.squareup.okhttp3:okhttp:4.9.0")
}

configurations.all {
    resolutionStrategy.force "com.mikepenz:fastadapter:${versions.fastadapter}"
    resolutionStrategy.force "com.mikepenz:iconics-core:${versions.iconics}"
}