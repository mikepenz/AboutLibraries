plugins {
    id "com.android.lint"
    id "org.jetbrains.kotlin.jvm"
    id "com.gradle.plugin-publish" version "0.20.0"
    id "java-gradle-plugin"
    id "maven-publish"
    id "org.jetbrains.dokka"
}


group = "com.mikepenz.aboutlibraries.plugin"
version = rootProject.properties["version"]

gradlePlugin {
    plugins {
        aboutlibsPlugin {
            id = "${project.group}"
            implementationClass = "${project.group}.AboutLibrariesPlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/mikepenz/AboutLibraries'
    vcsUrl = 'https://github.com/mikepenz/AboutLibraries'
    description = 'Resolve all dependencies used in a gradle module, with associated license and further information.'

    plugins {
        aboutlibsPlugin {
            displayName = 'AboutLibraries Library Gradle Plugin'
            tags = ['libraries', 'licenses', 'android']
        }
    }

    mavenCoordinates {
        groupId = "${project.group}"
        artifactId = "aboutlibraries-plugin"
    }
}

repositories {
    google()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

kotlin {
    jvmToolchain(11)
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation(libs.kotlin.stdlib)

    // parser the pom.xml files
    implementation(libs.ivy.core)

    // add better android support
    compileOnly(libs.gradle.build)

    // lint rules
    lintChecks(libs.android.lint.gradle)
}

// generate zip file for android maven release tool
task packageFiles(type: Zip, dependsOn: 'uploadArchives') {
    def groupDir = rootProject.group.replaceAll('\\.', '/')
    from("../repo/$groupDir/$rootProject.name/$rootProject.version/")
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokkaGenerate) {
    archiveClassifier = 'javadoc'
    from "$buildDir/javadoc"
}

publishing {
    repositories {
        mavenLocal()
    }
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dokka {
    dokkaSourceSets {
    }
}